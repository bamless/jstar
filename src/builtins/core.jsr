// -----------------------------------------------------------------------------
// BUILTIN OBJECTS
// -----------------------------------------------------------------------------

var argv = []

// -----------------------------------------------------------------------------
// BUILTIN CLASSES
// -----------------------------------------------------------------------------

class Number
    native construct(n)
    native isInt()
    native __string__()
    native __hash__()
end

class Boolean
    native construct(b)
    native __string__()
    native __hash__()
end

class Null
    native __string__()
end

class Function
    native __string__()
    native bind(receiver)
    native arity()
    native vararg()
    native defaults()
    native getName()
end

class Module
    native __string__()
    native globals()
end

class StackTrace end
class Userdata end

class Iterable
    native join(sep="")

    fun collect(collector)
        return collector(this)
    end

    fun forEach(func)
        for var e in this
            func(e)
        end
    end

    fun sum()
        var res = 0
        for var e in this
            res += e
        end
        return res
    end

    fun reduce(init, fn)
        var acc = init
        for var e in this
            acc = fn(acc, e)
        end
        return acc
    end

    fun any(predicate)
        for var e in this
            if predicate(e)
                return true
            end
        end
        return false
    end

    fun all(predicate)
        for var e in this
            if !predicate(e)
                return false
            end
        end
        return true
    end

    fun enumerate(start=0)
        return enumerate(this, start)
    end

    fun concat(iterable)
        return concat(this, iterable)
    end

    fun map(fn)
        return map(this, fn)
    end

    fun filter(predicate)
        return filter(this, predicate)
    end

    fun skip(count)
        return skip(this, count)
    end

    fun take(count)
        return take(this, count)
    end

    fun zip(iterable)
        return zip(this, iterable)
    end

    fun sorted()
        var lst = List(this)
        lst.sort()
        return lst
    end

    fun apply(collector, ...)
        return collector(this, args)...
    end
end

class Sequence is Iterable
    fun contains(e)
        return this.indexOf(e) != -1
    end

    fun indexOf(e)
        var length = #this
        for var i = 0; i < length; i += 1
            if this[i] == e
                return i
            end
        end
        return -1
    end

    fun indexOfLast(e)
        for var i = #this - 1; i >= 0; i -= 1
            if this[i] == e
                return i
            end
        end
        return -1
    end

    fun count(e)
        var count = 0
        for var o in this
            if o == e 
                count += 1
            end
        end
        return count
    end

    fun empty()
        return #this == 0
    end

    fun reversed()
        return ReversedSequence(this)
    end

    fun __eq__(other)
        if type(other) != type(this)
            return false 
        end

        var length = #this
        if length != #other
            return false
        end

        for var i = 0; i < length; i += 1
            if this[i] != other[i]
                return false
            end
        end
        return true
    end
end

static var GEN_SEND, GEN_THROW, GEN_RETURN = 0, 1, 2

class Generator is Iterable
    fun send(val=null)
        return this(val, GEN_SEND)
    end

    fun throw(exc)
        return this(exc, GEN_THROW)
    end

    fun close(val=null)
        return this(val, GEN_RETURN)
    end

    fun __iter__(_)
        this()
        return !this.isDone()
    end

    native __next__(_)
    native __string__()
    native isDone()
end

class String is Sequence
    native construct(...)
    native charAt(idx)
    native startsWith(prefix, offset=0)
    native endsWith(suffix)
    native split(separator)
    native strip()
    native chomp()
    native escaped()
    native __mul__(reps)
    native __mod__(args)
    native __eq__(o)
    native __len__()
    native __iter__(iter)
    native __next__(idx)
    native __string__()
    native __hash__()

    fun __rmul__(reps)
        return this.__mul__(reps)
    end
end

class List is Sequence
    native construct(n=0, init=null)
    native add(e)
    native insert(i, e)
    native removeAt(i)
    native clear()
    native sort(comparator=null)
    native __len__()
    native __add__(other)
    native __eq__(other)
    native __iter__(iter)
    native __next__(idx)

    fun addAll(iterable)
        var changed = false
        for var e in iterable
            this.add(e)
            changed = true
        end
        return changed
    end

    fun insertAll(iterable, i=0)
        var changed = false
        for var e in iterable
            this.insert(i, e)
            changed = true
        end
        return changed
    end

    fun remove(e)
        var length = #this
        for var i = 0; i < length; i += 1
            if e == this[i]
                this.removeAt(i)
                return true
            end
        end
        return false
    end

    fun removeAll(iterable)
        var changed = false
        for var e in iterable
            var r = this.remove(e)
            changed = changed or r
        end
        return changed
    end

    fun pop()
        return this.removeAt(#this - 1)
    end

    fun __string__()
        return "[" + this.join(", ") + "]"
    end
end

class Tuple is Sequence
    native construct(iterable=null)
    native __len__()
    native __add__(other)
    native __eq__(other)
    native __iter__(iter)
    native __next__(idx)
    native __hash__()

    fun __string__()
        return "(" + this.join(", ") + ")"
    end
end

class Table is Iterable
    native construct(iterable=null)
    native delete(key)
    native clear()
    native contains(key)
    native keys()
    native values()
    native __get__(key)
    native __set__(key, val)
    native __len__()
    native __iter__(i)
    native __next__(i)
    native __string__()
end

class Enum
    native construct(...)
    native value(name)
    native name(value)
end

// -----------------------------------------------------------------------------
// BUILTIN FUNCTIONS
// -----------------------------------------------------------------------------

fun assert(cond, msg="assertion failed", exception=null)
    if !cond 
        raise (exception(msg) if exception else AssertException(msg))
    end
end

fun typeAssert(arg, cls, name)
    if !(arg is cls)
        var got, expected = cls.getName(), type(arg).getName()
        raise TypeException("{0} must be a {1}, got {2}" % (name, got, expected))
    end
end

fun partial(fn, arg, ...)
    var rest = args
    return (|...| => fn(arg, rest + args)...) if #rest != 0 else (|...| => fn(arg, args)...)
end

fun compose(fn1, fn2, ...)
    var functions = args.reversed().concat((fn2, fn1)).collect(Tuple)
    var second, first = functions
    return |...| => functions.skip(2).reduce(first(second(args)...), |ret, fn| => fn(ret))
end

native ascii(num)
native char(c)
native eval(source)
native garbageCollect()
native int(n)
native print(s, ...)
native type(o)

// -----------------------------------------------------------------------------
// BUILTIN ITERATORS
// -----------------------------------------------------------------------------

class ReversedSequence is Sequence
    construct(sequence)
        this._sequence = sequence
        this._size = #sequence
    end

    fun __get__(i)
        try
            return this._sequence[this._size - i - 1]
        except IndexOutOfBoundException e
            raise IndexOutOfBoundException(##i, e)
        end
    end

    fun __len__()
        return this._size
    end

    fun __iter__(idx)
        if idx == null
            return 0 if this._size > 0 else false
        end
        return idx + 1 if idx < this._size - 1 else false
    end

    fun __next__(idx)
        return this._sequence[this._size - idx - 1]
    end
end

fun range(start, stop, step=1)
    assert(step != 0, "step must be different than 0", InvalidArgException)
    if step > 0
        for var i = start; i < stop; i += step
            yield i
        end
    else
        for var i = start; i > stop; i += step
            yield i
        end
    end
end

fun enumerate(iterable, start=0)
    for var e in iterable
        yield (start, e)
        start += 1
    end
end

fun concat(iter1, iter2)
    for var e in iter1
        yield e
    end
    for var e in iter2
        yield e
    end
end

fun map(iterable, fn)
    for var e in iterable
        yield fn(e)
    end
end

fun filter(iterable, predicate)
    for var e in iterable
        if predicate(e)
            yield e
        end
    end
end

fun skip(iterable, count)
    for var e in iterable
        if count != 0
            count -= 1
            continue
        end
        yield e
    end
end

fun take(iterable, count)
    for var e in iterable
        if count == 0
            break
        end
        yield e
        count -= 1
    end
end

class zip is Iterable
    construct(iterable1, iterable2)
        this._iterable1 = iterable1
        this._iterable2 = iterable2
    end

    fun __iter__(iters)
        var iter1, iter2 = null, null
        if iters != null
            iter1, iter2 = iters
        end

        var res1 = this._iterable1.__iter__(iter1)
        if !res1
            return false
        end

        var res2 = this._iterable2.__iter__(iter2)
        if !res2
            return false
        end

        return res1, res2
    end

    fun __next__(iters)
        var iter1, iter2 = iters
        return this._iterable1.__next__(iter1), this._iterable2.__next__(iter2)
    end
end

// -----------------------------------------------------------------------------
// BUILTIN EXCEPTIONS
// -----------------------------------------------------------------------------

class Exception
    construct(err="", cause=null)
        this._err = err
        this._cause = cause
        this._stacktrace = null
    end

    fun err()
        return this._err
    end

    fun cause()
        return this._cause
    end

    native printStacktrace()
    native getStacktrace()
end

class TypeException is Exception end
class NameException is Exception end
class FieldException is Exception end
class MethodException is Exception end
class ImportException is Exception end
class StackOverflowException is Exception end
class SyntaxException is Exception end
class InvalidArgException is Exception end
class GeneratorException is Exception end
class IndexOutOfBoundException is Exception end
class AssertException is Exception end
class NotImplementedException is Exception end
class ProgramInterrupt is Exception end